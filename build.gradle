buildscript {

    repositories {

        google()

        mavenCentral()
    }

    dependencies {

        classpath 'com.android.tools.build:gradle:7.0.4'
    }
}

allprojects {

    repositories {

        google()

        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

ext {
    libraryVersion = '1.2.3'
    publishedGroupId = 'com.budiyev.android'
}

group = publishedGroupId
version = libraryVersion

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 31
        versionCode 24
        versionName libraryVersion
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api 'androidx.annotation:annotation:1.3.0'
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    options.charSet = 'UTF-8'
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifactId = 'circular-progress-bar'
                groupId = publishedGroupId
                version = libraryVersion
            }
        }
    }
}
